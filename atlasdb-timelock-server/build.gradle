apply from: "../gradle/publish-dists.gradle"
apply from: "../gradle/shared.gradle"

apply plugin: 'com.palantir.java-distribution'
apply plugin: 'org.inferred.processors'
apply plugin: 'org.unbroken-dome.test-sets'

testSets {
    integTest
}

jacocoTestReport {
    executionData test, integTest
}

check.dependsOn integTest

dependencies {
    compile project(":timestamp-api")
    compile project(":lock-impl")

    compile group: 'com.github.rholder', name: 'guava-retrying'
    compile group: 'com.palantir.remoting1', name: 'jersey-servers'
    compile group: 'com.palantir.remoting1', name: 'ssl-config'
    compile group: 'io.atomix', name: 'atomix'
    compile group: 'io.atomix.catalyst', name: 'catalyst-netty'
    compile group: 'io.dropwizard', name: 'dropwizard-core'

    processor group: 'org.immutables', name: 'value'

    testCompile project(':atlasdb-config')

    testCompile group: 'org.assertj', name: 'assertj-core'
    testCompile group: 'org.mockito', name: 'mockito-core'

    integTestCompile group: 'io.dropwizard', name: 'dropwizard-testing'
}

def applicationName = "atlasdb-timelock-server"

distribution {
    serviceName applicationName
    mainClass 'com.palantir.atlasdb.timelock.TimeLockServer'
    args 'server', 'var/conf/timelock.yml'
}

tasks.getByName('distTar').doLast {
    println "Distribution tarball is ready at $outputs.files.singleFile"
}

publishing.publications {
    artifactory(MavenPublication) {
        artifact distTar
        artifactId applicationName
        groupId 'com.palantir.atlasdb'
    }
}
